    (* score, part, calls, etc. *)

score                   = <ows> clj-expr* part+
part                    = calls <ows> music-data
calls                   = name
                          (<ows> <"/"> <ows> name)*
                          (<ows> nickname)?
                          <ows> <":">
<acceptable-name>       = #"[a-zA-Z]{2}[\w\-+'()]*"
name                    = acceptable-name
nickname                = <"\""> acceptable-name <"\"">

    (* notes, chords & other events *)

<music-data>            = (voices | event)*
<event>                 = chord | note | rest | octave-change |
                          clj-expr | marker | at-marker | barline
voices                  = voice+ (<voice-zero> | <#"\z"> | &part)
voice                   = voice-number event*
voice-number            = <"V"> #"[1-9]\d*" <":"> <ows>
<voice-zero>            = <"V0:"> <ows>

chord                   = (note | rest) subchord+
<subchord>              = <"/"> <ows> (octave-change | clj-expr)*
                                <ows> (note | rest) <ows>
note                    = pitch duration? <ows> slur?
rest                    = <"r"> duration? <ows>

pitch                   = !name #"[a-g][+-]*"
duration                = note-length <ows> barline? <ows> subduration* slur?
<subduration>           = tie <ows> barline? <ows> note-length <ows> barline? <ows>
note-length             = number dots?
number                  = #"-?[0-9]+"
dots                    = #"\.+"
<tie>                   = <"~">
slur                    = <"~">

<octave-change>         = (octave-set | octave-up | octave-down) <ows>
octave-set              = <"o"> number
octave-up               = <">">
octave-down             = <"<">

marker                  = <"%"> name <ows>
at-marker               = <"@"> name <ows>

barline                 = <"|"> <ows>

    (* inline clojure expressions *)

clj-character           = <"\\"> ( "newline" / "space" / "tab" / 
                                   "formfeed" / "backspace" / "return" /
                                   #"(o|u)\d{4}" / #"." )

clj-string              = <"\""> inside-clj-string* <"\"">
<inside-clj-string>     = !"\"" #".|\n|\r" | "\\\"" 

<inside-clj-coll>       = !( "(" | ")" | 
                             "[" | "]" | 
                             "#{" | 
                             "{" | "}" | 
                             "\"" | "\\" )
                          #".|\n|\r" | clj-list | clj-string | clj-character |
                          clj-vector | clj-map | clj-set

clj-expr                = <"("> inside-clj-coll* <")"> <ows>
clj-list                = <"("> inside-clj-coll* <")">
clj-vector              = <"["> inside-clj-coll* <"]">
clj-set                 = <"#{"> inside-clj-coll* <"}">
clj-map                 = <"{"> inside-clj-coll* <"}">

    (* comments & whitespace *)

ows                     = (#"\s" | comment)*
comment                 = "#" #".*" #"(\n|\r|$)+"
