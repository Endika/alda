    (* score, part, calls, etc. *)

score                   = <ows> clj-expr* part+
part                    = calls <ows> music-data
calls                   = name
                          (<ows> <"/"> <ows> name)*
                          (<ows> nickname)?
                          <ows> <":">
<acceptable-name>       = #"[a-zA-Z]{2}[\w\-+'()]*"
name                    = acceptable-name
nickname                = <"\""> acceptable-name <"\"">

    (* notes, chords & other events *)

<music-data>            = <ows> (voices | event)*
<event>                 = chord | note | rest | octave-change |
                          clj-expr | marker | at-marker | barline |
                          code-block | cram
voices                  = voice+ (<voice-zero> | <#"\z"> | &part)
voice                   = voice-number event*
voice-number            = <"V"> #"[1-9]\d*" <":"> <ows>
<voice-zero>            = <"V0:"> <ows>

chord                   = (note | rest) subchord+
<subchord>              = <"/"> <ows> (octave-change | clj-expr)*
                                <ows> (note | rest) <ows>
note                    = pitch duration? <ows> slur?
rest                    = <"r"> duration? <ows>

pitch                   = !name #"[a-g]" accidental*
<accidental>            = flat | sharp | natural
flat                    = "-"
sharp                   = "+"
natural                 = "="
duration                = note-length <ows> barline? <ows> subduration* slur?
<subduration>           = tie <ows> barline? <ows> note-length <ows> barline? <ows>
note-length             = positive-number dots?
number                  = positive-number | negative-number
positive-number         = #"[0-9]+"
negative-number         = #"-[0-9]+"
dots                    = #"\.+"
<tie>                   = <"~">
slur                    = <"~">

<octave-change>         = (octave-set | octave-up | octave-down) <ows>
octave-set              = <"o"> number
octave-up               = <">">
octave-down             = <"<">

marker                  = <"%"> name <ows>
at-marker               = <"@"> name <ows>

barline                 = <"|"> <ows>

    (* inline clojure expressions *)

clj-character           = <"\\"> ( "newline" / "space" / "tab" / 
                                   "formfeed" / "backspace" / "return" /
                                   #"(o|u)\d{4}" / #"." )

clj-string              = <"\""> inside-clj-string* <"\"">
<inside-clj-string>     = !"\"" #".|\n|\r" | "\\\"" 

clj-expr                = <"("> inside-clj-expr* <")"> <ows>
<inside-clj-expr>       = !( "(" | ")" | "\"" | "\\" )
                          #".|\n|\r" | clj-string | clj-character | clj-expr

    (* alda code blocks *)

code-block              = code-block-no-ws <ows>
code-block-no-ws        = <"["> inside-code-block* <"]">
<inside-code-block>     = !( "[" | "]") #".|\n|\r" | code-block-no-ws

    (* cram - rhythmic section *)

cram                    = <"{"> event* <"}"> duration? <ows>

    (* comments & whitespace *)

ows                     = (#"\s" | comment)*
comment                 = "#" #".*" #"(\n|\r|$)+"
